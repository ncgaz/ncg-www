PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX geojson: <https://purl.org/geojson/vocab#>
PREFIX nct:     <http://n2t.net/ark:/39333/ncg/type#>
PREFIX ncv:     <http://n2t.net/ark:/39333/ncg/vocab#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX skosxl:  <http://www.w3.org/2008/05/skos-xl#>

SELECT
?placeid
?placename
?description
?location
?bordering_county
(GROUP_CONCAT(DISTINCT ?type;separator="|") as ?types)
(GROUP_CONCAT(DISTINCT ?county;separator="|") as ?counties)
(GROUP_CONCAT(DISTINCT ?altname;separator="|") as ?altnames)
(GROUP_CONCAT(DISTINCT ?offname;separator="|") as ?offnames)

WHERE {
  ?typeuri a skos:Concept ;
    (skosxl:prefLabel/skosxl:literalForm) ?typename .

  ?placeuri
    dcterms:type ?typeuri ;
    skos:prefLabel ?placename ;
    skos:note ?note .

  OPTIONAL {
    ?placeuri skos:altLabel ?altname .
  }

  OPTIONAL {
    ?placeuri ncv:offensiveLabel ?offname .
  }

  OPTIONAL {
    ?placeuri ncv:county ?countyuri .

    ?countyuri
      dcterms:type nct:county ;
      skos:prefLabel ?countyname .
  }

  OPTIONAL {
    ?placeuri geojson:geometry ?location  .
  }

  FILTER NOT EXISTS { ?placeuri dcterms:type nct:county }

  BIND (EXISTS {
    ?placeuri dcterms:type nct:borderingCounty .
  } AS ?bordering_county)

  BIND (REPLACE(STR(?placeuri),  "^(.*)/([^/]*)$", "$2") AS ?placeid)
  BIND (REPLACE(STR(?typeuri),   "^(.*)/([^/]*)$", "$2") AS ?typeid)
  BIND (REPLACE(STR(?countyuri), "^(.*)/([^/]*)$", "$2") AS ?countyid)

  BIND (CONCAT(?typeid, ">", ?typename) AS ?type)
  BIND (CONCAT(?countyid, ">", ?countyname) AS ?county)
  BIND (CONCAT(IF(REGEX(?note, "^[A-Z]"), "", "â€¦ "), ?note) AS ?description)
}

GROUP BY
?placeid
?placename
?description
?location
?bordering_county
