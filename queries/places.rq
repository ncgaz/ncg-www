PREFIX afn:     <http://jena.apache.org/ARQ/function#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX nct:     <http://n2t.net/ark:/39333/ncg/type#>
PREFIX ncv:     <http://n2t.net/ark:/39333/ncg/vocab#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX skosxl:  <http://www.w3.org/2008/05/skos-xl#>

SELECT
?placeid
?placename
?description
(GROUP_CONCAT(?type;separator="|") as ?types)

WHERE {
  ?typeuri a skos:Concept ;
    (skosxl:prefLabel/skosxl:literalForm) ?typename .

  ?placeuri
    dcterms:type ?typeuri ;
    skos:prefLabel ?placename ;
    skos:note ?note .

  FILTER NOT EXISTS { ?placeuri dcterms:type nct:county }
  FILTER NOT EXISTS {
    ?placeuri ncv:county ?countyuri .
    ?countyuri dcterms:type nct:county .
  }

  BIND (afn:localname(?placeuri) AS ?placeid)
  BIND (CONCAT(afn:localname(?typeuri), ">", ?typename) AS ?type)
  BIND (CONCAT(IF(REGEX(?note, "^[A-Z]"), "", "â€¦ "), ?note) AS ?description)
}

GROUP BY ?placeid ?placename ?description
